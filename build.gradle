ext {
	// Publishing
	sporkVersion = '4.0.0'
	sporkReleasing = false
	sporkUserOrg = 'bytewelder'
	sporkWebsite = 'https://github.com/ByteWelder/Spork'
	sporkGroupId = 'com.bytewelder.spork'
	sporkRepoName = sporkReleasing ? 'maven-release' : 'maven-snapshot'
	sporkName = 'Poetry'

	// Dependencies
	junitVersion = '4.12'
	mockitoVersion = '2.7.14'
	findBugsVersion = '3.0.1'

	// Android settings and dependencies
	androidBuildToolsVersion = '25.0.2'
	androidCompileSdkVersion = 25
	androidMinSdkVersion = 15
	androidTargetSdkVersion = 25
	androidSupportLibraryVersion = '25.3.1'
	androidSupportEspressoVersion = '2.2.2'
	androidSupportTestRunnerVersion = '0.5'
}

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:2.3.1'
		classpath 'org.jacoco:org.jacoco.core:0.7.7.201606060606'
		classpath 'com.novoda:bintray-release:0.4.0'
	}
}

subprojects {

	repositories {
		mavenLocal()
		jcenter()
	}

	apply plugin: 'jacoco'
	apply plugin: 'checkstyle'
	apply plugin: 'findbugs'

	checkstyle {
		configFile = new File(rootDir, 'config/checkstyle.xml')
	}

	findbugs {
		effort = 'default'
		reportLevel = 'medium'
		excludeFilter = new File(rootDir, 'config/findbugs-excludes.xml')
	}

	group = sporkGroupId
	version = sporkVersion

	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
		}
	}

	tasks.withType(FindBugs) {
		reports {
			xml.enabled false
			html.enabled true
		}
	}

	tasks.withType(JavaCompile) {
		sourceCompatibility = '1.7'
		targetCompatibility = '1.7'
	}
}
