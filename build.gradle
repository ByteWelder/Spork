buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.novoda:bintray-release:0.3.4'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.0'
        // bintray usage: ./gradlew clean build bintrayUpload -PbintrayUser=??? -PbintrayKey=??? -PdryRun=false
    }
}

repositories {
    jcenter()
    maven {
        url "https://dl.bintray.com/sporklibrary/spork"
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'bintray-release'

ext {
    // Publishing
    sporkVersion = '1.0.0'
    sporkUserOrg = 'sporklibrary'
    sporkWebsite = 'https://sporklibrary.github.io'
    sporkGroupId = 'io.github.sporklibrary'
    sporkRepoName = 'spork'
    sporkName = 'Spork Mockito'
    sporkArtifactId = 'spork-mockito'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

// Fixes jar name
archivesBaseName = sporkArtifactId

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = sporkVersion

dependencies {
	compile (
        [group: 'io.github.sporklibrary', name: 'spork', version: '2.4.0'],
        [group: 'org.mockito', name: 'mockito-core', version: '1.+']
    )

	testCompile (
		[group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3'],
		[group: 'ch.qos.logback', name: 'logback-core', version: '1.1.3'],
		[group: 'ch.qos.logback', name: 'logback-access', version: '1.1.3'],

		[group: 'junit', name: 'junit', version: '4.12']
	)
}

publish {
    userOrg = sporkUserOrg
    repoName = sporkRepoName
    groupId = sporkGroupId
    artifactId = sporkArtifactId
    version = sporkVersion
    description = sporkName
    website = sporkWebsite
}

task javadocJar(type: Jar) {
	archiveName = sporkArtifactId + "-" + sporkVersion + "-" + "javadoc.jar"
 	from('build/docs/javadoc') {
		include '**'
	}
}
