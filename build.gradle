ext {
	// Publishing
	sporkVersion = '4.0.0'
	sporkReleasing = false
	sporkUserOrg = 'bytewelder'
	sporkWebsite = 'https://github.com/ByteWelder/Spork'
	sporkGroupId = 'com.bytewelder.spork'
	sporkRepoName = sporkReleasing ? 'maven-release' : 'maven-snapshot'
	sporkName = 'Poetry'

	// Dependencies
	junitVersion = '4.12'
	mockitoVersion = '2.7.9'
	findBugsVersion = '3.0.1'
}

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:2.3.0'
		classpath 'org.jacoco:org.jacoco.core:0.7.7.201606060606'
		classpath 'com.novoda:bintray-release:0.4.0'
	}
}

subprojects {

	repositories {
		mavenLocal()
		jcenter()
	}

	apply plugin: 'jacoco'
	apply plugin: 'checkstyle'
	apply plugin: 'findbugs'
	apply plugin: 'pmd'

	checkstyle {
		configFile = new File(rootDir, 'config/checkstyle.xml')
	}

	findbugs {
		excludeFilter = new File(rootDir, 'config/findbugs-excludes.xml')
	}

	group = sporkGroupId
	version = sporkVersion

	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
		}
	}

	// TODO: Implement for Android tasks
	tasks.withType(FindBugs) {
		reports {
			xml.enabled false
			html.enabled true
		}
	}
}
