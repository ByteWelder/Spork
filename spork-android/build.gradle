apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'bintray-release'

android {
	compileSdkVersion androidCompileSdkVersion
	buildToolsVersion androidBuildToolsVersion

	defaultConfig {
		minSdkVersion androidMinSdkVersion
		targetSdkVersion androidTargetSdkVersion
		versionCode 1
		versionName sporkVersion
		testInstrumentationRunner 'spork.android.test.TestRunner'
		testHandleProfiling true
		testFunctionalTest true
		consumerProguardFiles 'proguard.pro'
	}

	buildTypes {
		release {
			testCoverageEnabled false
		}
		debug {
			testCoverageEnabled true
		}
	}
}

dependencies {

	if (rootProject.hasProperty('sporkReleaseTarget') && rootProject.sporkReleaseTarget == 'spork-android') {
		compile "$sporkGroupId:spork:$sporkVersion"
	} else {
		compile project(':spork')
	}

	testCompile project(':support:spork-matchers')
	testCompile "junit:junit:$junitVersion"
	testCompile "org.mockito:mockito-core:$mockitoVersion"

	androidTestCompile "com.android.support.test:runner:$androidSupportTestRunnerVersion"
	androidTestCompile "com.android.support.test.espresso:espresso-core:$androidSupportEspressoVersion"
	androidTestCompile "com.android.support.test.espresso:espresso-contrib:$androidSupportEspressoVersion"
}

publish {
	userOrg = sporkUserOrg
	repoName = sporkRepoName
	groupId = sporkGroupId
	artifactId = project.name
	version = sporkVersion
	description = sporkName
	website = sporkWebsite
	issueTracker = sporkIssueTracker
	repository = sporkRepository
	autoPublish = sporkAutoPublish
	dryRun = sporkPublishIsDryRun
}

apply from: rootProject.file('gradle/release-android.gradle')
apply from: rootProject.file('gradle/findbugs-android.gradle')
apply from: rootProject.file('gradle/pmd-android.gradle')